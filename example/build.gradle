plugins {
    id 'org.springframework.boot' version '1.5.4.RELEASE'
    id 'org.sonarqube' version '2.5'
    id 'org.hidetake.ssh' version '2.7.0'
}

group = 'uk.ac.ebi.ena.ena-example'
version = '1.0.0'
sourceCompatibility = 1.8

repositories {
    mavenLocal()
    maven { url "http://ena-dev:8081/artifactory/all" }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.2'
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('de.codecentric:spring-boot-admin-starter-client:1.3.7')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('de.siegmar:logback-gelf:1.0.4')
    compile('io.springfox:springfox-swagger2:2.+')
    compile('io.springfox:springfox-swagger-ui:2.+')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

jar.manifest {
    attributes('Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Source-Compatibility': project.sourceCompatibility
    )
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            credentials {
                username artifactoryUsername
                password artifactoryPassword
            }
            url "http://ena-dev:8081/artifactory/libs-release-local"
        }
    }
}

processResources {
    filesMatching('application.properties') {
        expand(project.properties)
    }
}

remotes {
    dev {
        host = 'ves-ebi-5b'
        user = 'ena_adm'
        identity = file("${System.properties['user.home']}/.ssh/id_rsa")
    }
    test {
        host = 'ves-ebi-5a'
        user = 'ena_adm'
        identity = file("${System.properties['user.home']}/.ssh/id_rsa")
    }
    prodA {
        host = 'ves-hx-5a'
        user = 'ena_adm'
        identity = file("${System.properties['user.home']}/.ssh/id_rsa")
    }
    prodB {
        host = 'ves-hx-5b'
        user = 'ena_adm'
        identity = file("${System.properties['user.home']}/.ssh/id_rsa")
    }
}

task deployToDev << {
    ssh.run {
        session(remotes.dev) {
            execute "~/ena/generic-deploy.sh $project.name $version dev 1234 example"
        }
    }
}

task deployToTest << {
    ssh.run {
        session(remotes.test) {
            execute "~/ena/generic-deploy.sh $project.name $version test 1234 example"
        }
    }
}

task deployToProd << {
    ssh.run {
        session(remotes.prodA) {
            execute "~/ena/generic-deploy.sh $project.name $version prod 1234 example"
        }
    }
    ssh.run {
        session(remotes.prodB) {
            execute "~/ena/generic-deploy.sh $project.name $version prod 1234 example"
        }
    }
}